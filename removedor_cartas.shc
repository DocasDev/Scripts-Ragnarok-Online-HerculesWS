//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: removedor_cartas.txt                   |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Descrição: Separa cartas dos equipamentos (Removedor)  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 2.0 Modificação do sistema de remorção [QuodDG]        |
//   |--------------------------------------------------------|
//   | *Anotações                                             |
//   | -Acrescentado opções para remover cartas de todos os   |
//   | tipos de equipamento                                   |
//   | -Diálogo todo em português                             |
//   | -Rates modificadas                                     |
//   | -0% de destruir cartas para player VIP                 |
//   | -Sem perda de refinamento                              |
//   \________________________________________________________/

geffen,120,40,5	script	Removedor de Cartas#0	4_M_MASKMAN,{
	disable_items;
	if (MaxWeight - Weight < 10000) {
		mes "^FF8B00[= Removedor de Cartas =]^000000";
		mes "Não pode continuar porque você tem muitos objetos pesados. Vamos tentar continuar após a redução do peso.";
		close;
	}
	callfunc "F_REMOVERCARTAS";
}

function	script	F_REMOVERCARTAS	{	
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Olá!!";
	mes "Seja Bem-vindo ao novo Sistema de Remoção de Carta!";
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Nosso sistema foi aprimorado e agora dispomos de mais opções para você.";
	mes "O que deseja fazer?";
	next;
	menu "Mais informações",info,"Remover cartas",removercartas,"Nada...",fim;
	
info:
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Nosso novo sistema agora possui várias opções para você.";
	mes "Vejamos as novidades...";
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "-A chance do equipamento e cartas serem destruídos diminuiu para 30%.";
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "-Se você for VIP a chance de perder o equipamento e as cartas é zero.";
	mes "-Retiramos qualquer carta de todos os tipos de equipamentos.";
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "-Cartas de MVP não necessitam de itens especiais para serem removidas, apenas custam o dobro do preço.";
	mes "-Sem risco de perda de refinamento.";
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Esse é o valor do serviço:";
	mes "-Em Zeny -> 100.000z";
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Estamos trabalhando em mais melhorias.";
	mes "Enquanto isso aproveite nosso NOVO sistema de remorção de cartas.";
	close2;
	end;
	
removercartas:
	set .@preco_c,20; //preço em CASH
	set .@preco_z,100000; //preço em Zeny
	set .@pcnt_de,30; //porcentagem do equipamento ser destruído
	set .@pcnt_dc,30; //porcentagem da carta ser destruída
	set @usar_cash,0; //checagem se o jogador vai usar cash como pagamento
	set @mult_preco,1; //valor a ser multiplicado ao valor total do serviço. Caso a carta seja de um MVP o preço será duplicado
	setarray @menu$,""; //menu que guardará os itens que estão equipados no jogador
	setarray @equip_ident$[0],"Capacete","Armadura","Arma Esquerda","Arma Direita/Escudo","Manto","Botas","Acessório Esquerdo","Acessório Direito","Mascara/Óculos","Mascara/Asas/Aura";
	setarray .@equip_slot[0], EQI_HEAD_TOP,EQI_ARMOR,EQI_HAND_L,EQI_HAND_R,EQI_GARMENT,EQI_SHOES,EQI_ACC_L,EQI_ACC_R,EQI_HEAD_MID,EQI_HEAD_LOW;
	
	for(set .@i,0; .@i<getarraysize(.@equip_slot); set .@i,.@i+1) {
		if(getequipisequiped(.@equip_slot[.@i])){
			set @menu$,@menu$+getequipname(.@equip_slot[.@i])+":";
		}else{
			set @menu$,@menu$+"^999999"+@equip_ident$[.@i]+" (Vazio)^000000:";
		}
	}
	
	if(@menu$ == ""){
		mes "^FF8B00[= Removedor de Cartas =]^000000";
		mes "Desculpe, mas você não tem nada equipado no momento.";
		close;
	}
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "De qual equipamento você deseja retirar a(s) carta(s)?";
	next;
	set .@i,select("Nenhum:"+@menu$);
	switch(.@i){
		case 1:
			mes "^FF8B00[= Removedor de Cartas =]^000000";
			mes "É sempre um prazer serví-lo.";
			mes "Não esqueça de olhar as novidade de nosso NOVO sistema de recomorção de cartas.";
			close;
			break;
		default:
			set @equip_num,.@equip_slot[.@i-2];
			if(!getequipisequiped(@equip_num)){
				mes "^FF8B00[= Removedor de Cartas =]^000000";
				mes "Não há nada aqui. Talvez você tenha retirado o equipamento.";
				close;
			}
			break;
	}
	
	setarray .@equip_card,getequipcardid(@equip_num,0),getequipcardid(@equip_num,1),getequipcardid(@equip_num,2),getequipcardid(@equip_num,3);
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		if (.@equip_card[.@i] >= 4700) // Armor Enchant System
			set .@equip_card[.@i],0;
	}
	
	if (.@equip_card[0] <= 0 && .@equip_card[1] <= 0 && .@equip_card[2] <= 0 && .@equip_card[3] <= 0){
		mes "^FF8B00[= Removedor de Cartas =]^000000";
		mes "O equipamento que você escolheu não contem nenhuma carta.";
		mes "Escolha um que tenha carta(s) e poderei trabalhar.";
		close;
	}
	
	set .@mvp_list$,
		"|1038|1039|1046|1059|1086"+
		"|1087|1112|1115|1147|1150"+
		"|1157|1159|1190|1251|1252"+
		"|1272|1312|1373|1389|1399"+
		"|1418|1492|1502|1511|1583"+
		"|1623|1630|1646|1647|1648"+
		"|1649|1650|1651|1658|1685"+
		"|1688|1708|1719|1734|1751"+
		"|1766|1767|1768|1779|1785"+
		"|1813|1816|1817|1832|1871"+
		"|1874|1876|1885|1917|1980"+
		"|2022|2066|2068|2087|2094"+
		"|2095|2096|2097|2098|2099"+
		"|2100|2101|2102|2103|2104"+
		"|2105|2106|2107|2108|2109"+
		"|2110|2111|2112|2113|2131"+
		"|2156|2165|2202|2235|2236"+
		"|2237|2238|2239|2240|2241"+
		"|2249|2251|2253|2255|2341"+
		"|2362|2529|2533|2534|2535"+
		"|2996|";
		/*"|4408|4128|4456|4168|4142"+  //Gloom_Under_Night_Card, Golden_Bug_Card, Nidhogg_Shadow_Card, Dark_Lord_Card, Doppelganger_Card
		"|4134|4137|4386|4407|4357"+  //Dracula_Card, Drake_Card, Detale_Card, Randgris_Card, B_Seyren_Card
		"|4146|4132|4147|4372|4145"+  //Maya_Card, Mistress_Card, Baphomet_Card, Bacsojin_Card, Berzebub_Card
		"|4374|4352|4367|4236|4425"+  //Apocalips_H_Card, B_Ygnizem_Card, B_Shecil_Card, Amon_Ra_Card, Atroce_Card
		"|4359|4123|4144|4135|4143"+  //B_Eremes_Card, Eddga_Card, Osiris_Card, Orc_Load_Card, Orc_Hero_Card
		"|4263|4131|4430|4276|4419"+  //Incant_Samurai_Card, Moonlight_Flower_Card, Ifrit_Card, Lord_Of_Death_Card, Ktullanux_Card
		"|4403|4399|4376|4441|4302"+  //Kiel_Card, Thanatos_Card, Lady_Tanee_Card, Fallen_Bishop_Card, Tao_Gunka_Card
		"|4305|4148|4318|4121|4365"+  //Turtle_General_Card, Pharaoh_Card, Knight_Windstorm_Card, Phreeoni_Card, B_Katrinn_Card
		"|4363|4324|4361|4330|4342|"; //B_Magaleta_Card, Garm_Card, B_Harword_Card, Dark_Snake_Lord_Card, Rsx_0806_Card
		*/
	if ((.@equip_card[0] && compare(.@mvp_list$,"|"+.@equip_card[0]+"|")) ||
	    (.@equip_card[1] && compare(.@mvp_list$,"|"+.@equip_card[1]+"|")) ||
	    (.@equip_card[2] && compare(.@mvp_list$,"|"+.@equip_card[2]+"|")) ||
	    (.@equip_card[3] && compare(.@mvp_list$,"|"+.@equip_card[3]+"|")))
		set @mult_preco,2;
	
	if(Zeny < .@preco_z * @mult_preco){
		mes "^FF8B00[= Removedor de Cartas =]^000000";
		mes "Você não tem Zeny sulficiente para esse serviço.";
		close;
	}
	set Zeny,Zeny - (.@preco_z * @mult_preco);
	
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Aqui vamos nós...";
	mes "...";
	mes "..";
	mes ".";
	next;
	
	set .@equip_id, getequipid(@equip_num);
	set .@equip_refine, getequiprefinerycnt(@equip_num);
	set .@broken_equi,1;
	set .@card_break,0;
	delequip @equip_num;
	
	if(rand(0,100) > .@pcnt_de || #QDOVIP){
		set .@broken_equi,0;
		getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,0,0;
	}
	
	for(set .@i,0; .@i<4; set .@i, .@i+1){
		if (.@equip_card[.@i]) {
			if(rand(0,100) > .@pcnt_dc || #QDOVIP)
				getitem .@equip_card[.@i],1;
			else
				set .@card_break,1;
		}
	}
	
	if (!.@broken_equi && .@card_break)
		specialeffect EF_LORD;
	else if (.@broken_equi && .@card_break)
		specialeffect EF_SUI_EXPLOSION;
	else if (!.@broken_equi && !.@card_break)
		specialeffect EF_FIREPILLAR;
	else
		specialeffect EF_MAXPOWER;
	
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "-- Resultado da Separação de Carta --";
	if (!.@broken_equi) {
		mes "Rachadura não ocorreu durante o processo de separação de carta.";
		mes "^0000FFSeparação de equipamento foi normal.^000000";
	} else {
		mes "Rachadura ocorreu durante o processo de separação carta.";
		mes "Equipamento foi danificado. ^ff0000Irrecuperável.^000000";
	}
	mes "-------------------";
	if (!.@card_break) {
		mes "Erosão da superfície não ocorreu durante o processo de separação da carta.";
		mes "^0000ffSeparação de carta bem-sucedida.^000000";
	} else {
		mes "Erosão da superfície ocorreu durante o processo de separação cartão.";
		mes "A carta foi danificada. ^ff0000Irrecuperável.^000000";
	}
	next;
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "Isso é tudo...";
	mes "Volte sempre!";
	close2;
	end;
	
fim:
	mes "^FF8B00[= Removedor de Cartas =]^000000";
	mes "É sempre um prazer serví-lo.";
	mes "Não esqueça de olhar as novidade de nosso NOVO sistema de recomorção de cartas.";
	close2;
	end;
}

malaya,116,218,6	duplicate(Removedor de Cartas#0)	Removedor de Cartas#31	4_M_MASKMAN