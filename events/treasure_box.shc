/* ===============================================================
                           Treasure Box Event
   ================================================================
   ----------------------------------------------------------------
                            COMPATIBILIDADE
							   Hercules
   ----------------------------------------------------------------
                                VERSÕES
                        1.0 Criação [QuodDG]
   ----------------------------------------------------------------
 * ================================================================
 */

-	script	TreasureBoxManager	-1,{

OnInit:
	//Tempo de respawn dos Baús.
	//Em horas
	$@qdo_tbe_respawn_time = 12;

	$@qdo_tbe_maps_columns = 2;
	setarray($@qdo_tbe_maps$[0],
	"moc_ruins",	"Andar 5",
	"prt_monk",		"Andar 15",
	"ra_temple",	"Andar 25"
	);

	//Configuração das Treasure Chest
	setarray($@qdo_tbe_mobs[0],
	TREASURE_BOX1,
	TREASURE_BOX2,
	TREASURE_BOX3,
	TREASURE_BOX4,
	TREASURE_BOX5,
	TREASURE_BOX6,
	TREASURE_BOX7,
	TREASURE_BOX8,
	TREASURE_BOX9,
	TREASURE_BOX10,
	TREASURE_BOX11,
	TREASURE_BOX12,
	TREASURE_BOX13,
	TREASURE_BOX14,
	TREASURE_BOX15,
	TREASURE_BOX16,
	TREASURE_BOX17
	);
	
	donpcevent("TreasureBoxManager::OnRespawn");
	
	end;

OnMinute00:
OnMinute30:
	donpcevent("TreasureBoxManager::OnRespawn");
	end;

OnRespawn:
	for(.@i = 0; .@i < getarraysize($@qdo_tbe_maps$); .@i += $@qdo_tbe_maps_columns){
		.@map_name$ = $@qdo_tbe_maps$[.@i];
		if(	callfunc("F_QDO_getd_tbe", .@map_name$) > gettimetick(2) || 
			mobcount(.@map_name$, "TreasureBoxManager::OnTreasureBoxBreaked") > 0){
			continue;
		}
		.@treasure_id = $@qdo_tbe_mobs[rand(getarraysize($@qdo_tbe_mobs))];
		callfunc("F_QDO_treasure_respawn", .@map_name$, .@treasure_id);
		announce("Um Baú do Tesouro apareceu no " + $@qdo_tbe_maps$[.@i + 1], bc_all);
	}
	end;

OnBreak:
	for(.@i = 0; .@i < getarraysize($@qdo_tbe_maps$); .@i += $@qdo_tbe_maps_columns){
		killmonster($@qdo_tbe_maps$[.@i], "TreasureBoxManager::OnTreasureBoxBreaked");
	}
	end;

OnTreasureBoxBreaked:
	.@map_name$ = strcharinfo(PC_MAP);
	callfunc("F_QDO_setd_tbe", .@map_name$, gettimetick(2) + ($@qdo_tbe_respawn_time * 3600));
	callfunc("F_QDO_get_tbe_map_info", .@map_name$, .@map_info$);
	announce(strcharinfo(PC_NAME) + " encontrou um Baú do Tesouro no " + .@map_info$[1], bc_all);
	end;
}

function	script	F_QDO_get_tbe_map_info	{
	for(.@i = 0; .@i < getarraysize($@qdo_tbe_maps$); .@i += $@qdo_tbe_maps_columns){
		if($@qdo_tbe_maps$[.@i] == getarg(0)){
			set getelementofarray(getarg(1), 0), $@qdo_tbe_maps$[.@i];
			set getelementofarray(getarg(1), 1), $@qdo_tbe_maps$[.@i + 1];
			return 1;
		}
	}
	return 0;
}

function	script	F_QDO_treasure_respawn	{
	monster(getarg(0), 0, 0, "Baú do Tesouro", getarg(1), 1, "TreasureBoxManager::OnTreasureBoxBreaked");
	return;
}

function	script	F_QDO_getd_tbe	{
	return getd("$" + getarg(0) + "_tbe");
}

function	script	F_QDO_setd_tbe	{
	setd("$" + getarg(0) + "_tbe", getarg(1));
	return;
}
